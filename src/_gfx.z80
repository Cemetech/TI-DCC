.module gfx
.local
_modulestart:

;------------------
Start:
	call SetLcdWindowFull
ReinitScreenmode:
	ld hl,incmode(INCR)+wrapmode(INCR)+movemode(HORIZ)+bgrmode(1)+origin(1)
	ld a,3
	jp _setlcdreg
;------------------
Exit:
	call SetLcdWindowFull
	ld hl,$1038
	ld a,$03
	jp _setlcdreg
;------------------
FillScreen: ;in: A=color
	push af
		call SetLcdWindowFull
		ld a,$22
		out ($10),a
		out ($10),a         ;set LCD write mode
	pop af
	ld bc,((320*240)/2)+256 ;Set to allow fullscreen coverage
_fillareaaligned:
	out ($11),a
	out ($11),a
_fillareaunaligned:
	out ($11),a
	out ($11),a
	dec c
	jp nz,_fillareaaligned
	djnz _fillareaaligned
	ret
;------------------
FillRect:  ;HL=X, A=Y, C=width-1, B=height-1, D=color
	push de
		push bc
			call _setrectregion
		pop de
		inc e
		inc d
		ld h,d
		call math.multEandH
		push hl
		pop bc
		ld a,$22
		out ($10),a
		out ($10),a         ;set LCD write mode
	pop af
	srl b
	rr  c
	inc b
	jp nc,_fillareaaligned
	jp _fillareaunaligned
;------------------
;NOTE: Base segment rectangle is always 64 pixels. DO NOT CHANGE SEGMENT AREA
FillSegment:  ;HL=X, A=Y, C=width-1, B=height-1, D=color
	push hl
		ld e,a    ;preserve A in E
		ld a,b
		cp c  ;h-w. carry if the box is wider than it is tall
		jr c,_fillseg_widebox_mode
_fillseg_tallbox_mode:
		ld hl,incmode(INCR)+wrapmode(INCR)+movemode(HORIZ)+bgrmode(1)+origin(1)
		dec e
		inc b
		inc b
		jr _fillseg_modecollect
_fillseg_widebox_mode:
		ld hl,incmode(INCR)+wrapmode(INCR)+movemode(VERT)+bgrmode(1)+origin(1)
		ex (sp),hl
		dec hl
		ex (sp),hl
		inc c
		inc c
_fillseg_modecollect:
		ld a,3
		push bc
			call _setlcdreg
		pop bc
	pop hl
	push de
		ld a,e
		call _setrectregion ;HL=X, A=Y, C=width-1, B=height-1
		ld a,$22
		out ($10),a
		out ($10),a         ;set LCD write mode
	pop de
	call _fillseg_corner
	ld a,d
	ld bc,(64/2)+256
	call _fillareaaligned
	ld a,d
	call _fillseg_corner
	jp ReinitScreenmode
	
_fillseg_corner:
	xor a
	out ($11),a
	out ($11),a
	ld a,d
	out ($11),a
	out ($11),a
	out ($11),a
	out ($11),a
	xor a
	out ($11),a
	out ($11),a
	ret
		
;------------------
;NOTE: SPRITE IS ASSUMED TO BE IN CURRENT MEMORY BANK ON INVOCATION
SpriteNoclip:  ;HL=X, A=Y, C=width-1, B=height-1, DE=spriteaddr
	push de
		push bc
			call _setrectregion
		pop de
		inc e
		inc d
		ld h,d
		call math.multEandH
		push hl
		pop bc
		ld a,$22
		out ($10),a
		out ($10),a         ;set LCD write mode
	pop de
	srl b
	rr  c
	inc b
	inc bc
	jr c,{+}
	dec bc
-: 	ld a,(de)
	inc de
	out ($11),a
	out ($11),a
+:  ld a,(de)
	inc de
	out ($11),a
	out ($11),a
	dec c
	jp nz,{-}
	djnz {-}
	ret
	
;------------------
;NOTE: SPRITE IS ASSUMED TO BE IN CURRENT MEMORY BANK ON INVOCATION
Sprite2BitNoclip:  ;HL=X, A=Y, C=width-1, B=height-1, DE=spriteaddr
	push de
		push bc
			call _setrectregion
		pop de
		inc e
		inc d
		srl e \ srl e \ srl e  ;width must be multiple of 8 so this is good.
		ld h,d
		call math.multEandH
		ld a,$22
		out ($10),a
		out ($10),a         ;set LCD write mode
	pop de
--:	ld a,(de)
	inc de
	ld c,a
	ld b,8
-:	rlc c
	ld a,XLC_DARKGRAY
	jr nc,{+}
	ld a,XLC_WHITE
+:	out ($11),a
	out ($11),a
	djnz {-}
	dec hl
	ld a,L
	or H
	jp nz,{--}
	ret
	
;------------------
_setrectregion: ;HL=X, A=Y, C=width-1, B=height-1
;note: 50=Ymin 51=Ymax 52=Xmin 53=Xmax
	ld d,a
	ld e,c
	ld a,$52
	call _setlcdreg  ;Xmin
	inc a
	ld c,e
	ld e,b
	ld b,0
	add hl,bc
	call _setlcdreg  ;Xmax
	ld h,b
	ld L,d
	ld a,$50
	call _setlcdreg  ;Ymin
	inc a
	ld d,b
	add hl,de
	call _setlcdreg  ;Ymax
	ld a,$20
	ld hl,0
	call _setlcdreg    ;reset cursor by writing to curX in origin mode
	ld a,$21
	ld hl,0
	jp _setlcdreg    ;reset cursor by writing to curY in origin mode
;------------------

SetLcdWindowFull:
	ld a,$50
	ld hl,0
	push hl
		call _setlcdreg  ;Ymin
		ld a,$52
		call _setlcdreg  ;Xmin
		dec a
		ld hl,239
		call _setlcdreg  ;Ymax
		ld a,$53
		ld hl,319
		call _setlcdreg  ;Xmax
	pop hl
	ld a,$20
	call _setlcdreg	 ;Y pos
	inc a
_setlcdreg:
	out ($10),a 
	out ($10),a
	ld c,$11
	out (c),h
	out (c),l
	ret 
	
;------------------

	




.echoln "MODULE gfx  SIZE : ",$-_modulestart
.endlocal
.endmodule